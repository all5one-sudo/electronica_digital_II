; CONTAR DE 00 A 99 INCREMENTANDO CON RB0 Y MOSTRAR EN DISPLAY


    LIST    P = 16F887
    #INCLUDE    <P16F887.INC>

	__CONFIG    _CONFIG1, _FOSC_INTRC_NOCLKOUT & _WDTE_OFF & _PWRTE_OFF & _MCLRE_ON & _CP_OFF & _CPD_OFF & _BOREN_ON & _IESO_ON & _FCMEN_ON & _LVP_ON
	__CONFIG    _CONFIG2, _BOR4V_BOR40V & _WRT_OFF
	
	
UNID_    EQU	    0x20    ; Unidades y decenas, con sus respectivos
DECEN_    EQU	    0x21    ; límites 
UNID_LIM   EQU	    0x22    ; 
DECEN_LIM   EQU	    0x23
DIS_ON    EQU	    0x24    ; Display a encender.
W_TEMP    EQU	    0x70    ; Registros para salvar contexto.
STATUS_TEMP    EQU  0x71
  
	    ORG	    0x00
	    GOTO    CONFIG_
	    
	    ORG	    0x04
	    GOTO    RUT_INT
	    
	    ORG	    0x05
	    
CONFIG_	    CLRF    UNID_	    ; Comienzo en 00 y voy hasta 99.
	    CLRF    DECEN_
	    MOVLW   0x0A
	    MOVWF   UNID_LIM
	    MOVWF   DECEN_LIM
	    CLRF    DIS_ON	    ; Comienzo con el display 0, y voy a
	    BANKSEL TRISA	    ; PORTA y PORTC serán outputs digitales.
	    CLRF    TRISA
	    BANKSEL TRISC
	    CLRF    TRISC
	    BANKSEL TRISB    
	    MOVLW   0X01
	    MOVWF   TRISB
	    BANKSEL ANSELH
	    CLRF    ANSELH
	    MOVLW .217
	    MOVWF TMR0
	    BANKSEL OPTION_REG	    
	    MOVLW   b'10000111'	   
	    MOVWF   OPTION_REG	;confg por flanco descende, y config ps
	    BANKSEL INTCON	    ; Habilito interrupciones globales, también
	    MOVLW   b'10110000'	    ; por RB0 y tmro
	    MOVWF   INTCON
	    BANKSEL PORTA	    ; Vuelvo al banco de PORTA para comenzar.
	    GOTO    MUX
 
MUX	    MOVF    DIS_ON,W
	    CALL    D_SELECT
	    MOVWF   PORTC
	    MOVF   DIS_ON,W
	    CALL    D_OR_U
	    MOVWF   FSR
	    MOVF    INDF,W
	    CALL    VALOR_DISP
	    MOVWF   PORTA
	    GOTO    MUX
 	    
VALOR_DISP  ADDWF   PCL,F	    ; Retorno el valor a mostrar por el display.
	    RETLW   B'00111111'	    ; 0
	    RETLW   B'00000110'	    ; 1
	    RETLW   B'01011011'	    ; 2
	    RETLW   B'01001111'	    ; 3
	    RETLW   B'01100110'	    ; 4
	    RETLW   B'01101101'	    ; 5
	    RETLW   B'01111101'	    ; 6
	    RETLW   B'00000111'	    ; 7
	    RETLW   B'01111111'	    ; 8
	    RETLW   B'01101111'	    ; 9
	    
D_SELECT    ADDWF   PCL,F	    ; Tabla para elegir qué display encender.
	    RETLW   B'11111110'
	    RETLW   B'11111101'

D_OR_U      ADDWF   PCL,F	    ; Tabla para mover a FSR la dirección de
	    RETLW   0x20	    ; las unidades o decenas según el display a
	    RETLW   0x21	    ; encender.
	    
	    
RUT_INT	    MOVWF   W_TEMP	    ; Salvo el estado de W y STATUS
	    SWAPF   STATUS,W	   
	    MOVWF   STATUS_TEMP		
	    BTFSC   INTCON,INTF   ;atiendo interrupciones por RBO/INT.
	    GOTO    INT_ON
	    BTFSS   INTCON,T0IF
	    GOTO FINISH
	    BCF INTCON, T0IF  ;multiplexado de displayy
	    MOVLW .217  ;recargo timer
	    MOVWF TMR0
	    INCF    DIS_ON
	    BTFSC   DIS_ON,1
	    CLRF    DIS_ON
	    GOTO FINISH
	    
INT_ON	    
	    BCF	    INTCON,INTF
	    INCF    UNID_,F
	    MOVF    UNID_,W
	    SUBWF   UNID_LIM,W
	    BTFSC   STATUS,Z
	    CALL    RESET_UNI
	    GOTO    FINISH
	    
RESET_UNI   CLRF    UNID_
	    INCF    DECEN_,F
	    MOVF    DECEN_,W
	    SUBWF   DECEN_LIM,W
	    BTFSC   STATUS,Z
	    CLRF    DECEN_
	    RETURN
	    
FINISH	    SWAPF   STATUS_TEMP,W ; Cargo contexto para volver y bajo la flag.
	    MOVWF   STATUS
	    SWAPF   W_TEMP,F
	    SWAPF   W_TEMP,W
	    RETFIE
	    
	    END
